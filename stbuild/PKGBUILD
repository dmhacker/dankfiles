# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
patches=("https://st.suckless.org/patches/scrollback/st-scrollback-0.8.diff"
         "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.diff"
         "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-0.8.diff")
source=("http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz"
        "config.h"
        "${patches[@]}")
sha256sums=('c4fb0fe2b8d2d3bd5e72763e80a8ae05b7d44dbac8f8e3bb18ef0161c7266926'
            '858353ecf78f5d2504594df41425ef36de30058ab14aa7c9c586b4b69c2d336f'
            '8279d347c70bc9b36f450ba15e1fd9ff62eedf49ce9258c35d7f1cfe38cca226'
            '3fb38940cc3bad3f9cd1e2a0796ebd0e48950a07860ecf8523a5afd0cd1b5a44'
            'e9f73c4de379d54ae107d0f4dd7b068a6628326c1bd7e11bfcd33d9079290347')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  # skip terminfo which conflicts with ncurses
  sed -i '/tic /d' Makefile
  cp $srcdir/config.h config.h
  for patch in "${patches[@]}"; do
    # rename config.def.h to config.h when applying patches
    sed -i 's/config.def.h/config.h/g' "$srcdir/$(basename $patch)"
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
