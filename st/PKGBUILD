# Maintainer: Jose Riha <jose1711 gmail com>
# Maintainer: Sebastian J. Bronner <waschtl@sbronner.com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.4
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=(libxft)
url=https://st.suckless.org
source=(https://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz
        https://st.suckless.org/patches/scrollback/st-scrollback-20201205-4ef0cbd.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20191024-a2c479c.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20200416-5703aa0.diff
        terminfo.patch
        README.terminfo.rst)
sha256sums=('d42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6'
            'SKIP'
            'SKIP'
            'SKIP'
            'f9deea445a5c6203a0e8e699f3c3b55e27275f17fb408562c4dd5d649edeea23'
            '0ebcbba881832adf9c98ce9fe7667c851d3cc3345077cb8ebe32702698665be2')
_sourcedir=$pkgname-$pkgver
_makeopts="--directory=$_sourcedir"

prepare() {
  patch --directory="$_sourcedir" --strip=0 < terminfo.patch

  cp "$BUILDDIR/config.h" "$_sourcedir"

  sed -i 's/config.def.h/config.h/g' st-scrollback-20201205-4ef0cbd.diff
  sed -i 's/config.def.h/config.h/g' st-scrollback-mouse-20191024-a2c479c.diff
  sed -i 's/config.def.h/config.h/g' st-scrollback-mouse-altscreen-20200416-5703aa0.diff
  patch --directory="$_sourcedir" --strip=1 < st-scrollback-20201205-4ef0cbd.diff
  patch --directory="$_sourcedir" --strip=1 < st-scrollback-mouse-20191024-a2c479c.diff
  patch --directory="$_sourcedir" --strip=1 < st-scrollback-mouse-altscreen-20200416-5703aa0.diff
}

build() {
  make $_makeopts X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  local installopts='--mode 0644 -D --target-directory'
  local shrdir="$pkgdir/usr/share"
  local licdir="$shrdir/licenses/$pkgname"
  local docdir="$shrdir/doc/$pkgname"
  make $_makeopts PREFIX=/usr DESTDIR="$pkgdir" install
  install $installopts "$licdir" "$_sourcedir/LICENSE"
  install $installopts "$docdir" "$_sourcedir/README"
  install $installopts "$docdir" README.terminfo.rst
  install $installopts "$shrdir/$pkgname" "$_sourcedir/st.info"
}
