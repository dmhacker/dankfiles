# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
patches=("https://st.suckless.org/patches/scrollback/st-scrollback-0.8.1.diff"
         "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.diff"
         "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-0.8.diff"
         "https://st.suckless.org/patches/anysize/st-anysize-0.8.1.diff")
source=("http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz"
        "config.h"
        "${patches[@]}")
sha256sums=('c4fb0fe2b8d2d3bd5e72763e80a8ae05b7d44dbac8f8e3bb18ef0161c7266926'
            '312ac6d827fe7365746f9f6ff1041df12cd0bc6a004676b1356d471cea991bb6'
            'ddc26e8e6dcce1a51f8aa8ed33072afcf4ff2d20f64ea24d96409479e86e8750'
            '3fb38940cc3bad3f9cd1e2a0796ebd0e48950a07860ecf8523a5afd0cd1b5a44'
            'bcfc106089d9eb75aa014d4915ed3e6842f1df54edd8b75597154096333df6fa'
            '8118dbc50d2fe07ae10958c65366476d5992684a87a431f7ee772e27d5dee50f')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # skip terminfo which conflicts with ncurses
  sed -i '/tic /d' Makefile

  cp $srcdir/config.h config.h

  # rename all occurences of config.def.h to config.h in patch files
  sed -i 's/config.def.h/config.h/g' "$srcdir/$(basename ${patches[0]})"
  sed -i 's/config.def.h/config.h/g' "$srcdir/$(basename ${patches[1]})"
  sed -i 's/config.def.h/config.h/g' "$srcdir/$(basename ${patches[2]})"

  for patch in "${patches[@]}"; do
    echo "Applying $patch ..."
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
